use crate::tokens::Token;
use crate::lexer::LexicalError;
use concrete_ast as ast;
use ast::common::Span;
use std::str::FromStr;


grammar;

extern {
  type Location = usize;
  type Error = LexicalError;

  enum Token {
    // keywords
    "let" => Token::KeywordLet,
    "const" => Token::KeywordConst,
    "fn" => Token::KeywordFn,
    "return" => Token::KeywordReturn,
    "struct" => Token::KeywordStruct,
    "if" => Token::KeywordIf,
    "else" => Token::KeywordElse,
    "while" => Token::KeywordWhile,
    "for" => Token::KeywordFor,
    "match" => Token::KeywordMatch,
    "mod" => Token::KeywordMod,
    "pub" => Token::KeywordPub,
    "mut" => Token::KeywordMut,
    "import" => Token::KeywordImport,

    // literals
    "identifier" => Token::Identifier(<String>),
    "integer" => Token::Integer(<u64>),
    "string" => Token::String(<String>),
    "boolean" => Token::Boolean(<bool>),

    // Other

    "(" => Token::LeftParen,
    ")" => Token::RightParen,
    "{" => Token::LeftBracket,
    "}" => Token::RightBracket,
    "[" => Token::LeftSquareBracket,
    "]" => Token::RightSquareBracket,
    "=" => Token::Assign,
    ";" => Token::Semicolon,
    ":" => Token::Colon,
    "->" => Token::Arrow,
    "," => Token::Coma,
    "<" => Token::LessThanSign,
    ">" => Token::MoreThanSign,
    ">=" => Token::MoreThanEqSign,
    "<=" => Token::LessThanEqSign,
    "." => Token::Dot,

    // operators
    "+" => Token::OperatorAdd,
    "-" => Token::OperatorSub,
    "*" => Token::OperatorMul,
    "/" => Token::OperatorDiv,
    "%" => Token::OperatorRem,
    "&&" => Token::OperatorAnd,
    "||" => Token::OperatorOr,
    "==" => Token::OperatorEq,
    "!=" => Token::OperatorNe,
    "!" => Token::OperatorNot,
    "~" => Token::OperatorBitwiseNot,
    "^" => Token::OperatorBitwiseXor,
    "&" => Token::OperatorBitwiseAnd,
    "|" => Token::OperatorBitwiseOr,
  }
}

// lalrpop macros

Dot<T>: Vec<T> = {
    <mut v:(<T> ".")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

SemiColon<T>: Vec<T> = {
    <mut v:(<T> ";")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

PlusSeparated<T>: Vec<T> = {
    <mut v:(<T> "+")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

// Requires the semicolon at end
SemiColonSeparated<T>: Vec<T> = {
  <T> ";" => vec![<>],
  <mut s:SemiColonSeparated<T>> <n:T> ";" => {
    s.push(n);
    s
  }
};

// -- Common

pub(crate) Ident: ast::common::Ident = {
  <lo:@L> <name:"identifier"> <hi:@R> => ast::common::Ident {
    name,
    span: ast::common::Span::new(lo, hi),
  }
}

pub(crate) TypeSpec: ast::types::TypeSpec = {
  <name:Ident> => ast::types::TypeSpec::Simple {
    name
  },
  <lo:@L> <name:Ident> "<" <type_params:Comma<TypeSpec>> ">" <hi:@R> => ast::types::TypeSpec::Generic {
    name,
    type_params,
    span: Span::new(lo, hi),
  }
}

pub(crate) GenericParam: ast::common::GenericParam = {
  <name:Ident> => ast::common::GenericParam {
    name,
    params: vec![],
  },
  <name:Ident> ":" <params: PlusSeparated<Ident>> => ast::common::GenericParam {
    name,
    params,
  },
}

pub(crate) GenericParams: Vec<ast::common::GenericParam> = {
  "<" <Comma<GenericParam>> ">" => <>
}

// Module list

pub Program: ast::Program = {
  <Module> => {
    ast::Program {
      modules: vec![<>],
    }
  },
  <mut s:Program> <n:Module> => {
      s.modules.push(n);
      s
  },
}

// Modules

pub(crate) Module: ast::modules::Module = {
  "mod" <name:Ident> "{" <imports:ImportList?> <contents:ModuleItems> "}" => {
    ast::modules::Module {
      doc_string: None,
      imports: imports.unwrap_or_else(Vec::new),
      name,
      contents
    }
  }
}

pub(crate) ImportList: Vec<ast::imports::ImportStmt> = {
  <ImportStmt> => vec![<>],
  <mut s:ImportList> <n:ImportStmt> => {
      s.push(n);
      s
  },
}


pub(crate) ImportStmt: ast::imports::ImportStmt = {
  "import" <module:Dot<Ident>> "{" <symbols:Comma<Ident>> "}" ";"  => {
    ast::imports::ImportStmt {
      module,
      symbols,
    }
  }
}


pub(crate) ModuleItems: Vec<ast::modules::ModuleDefItem> = {
  <ModuleDefItem> => vec![<>],
  <mut s:ModuleItems> <n:ModuleDefItem> => {
      s.push(n);
      s
  },
}

pub(crate) ModuleDefItem: ast::modules::ModuleDefItem = {
  <ConstantDef> ";" => {
    ast::modules::ModuleDefItem::Constant(<>)
  },
  <FunctionDef> => {
    ast::modules::ModuleDefItem::Function(<>)
  },
  <Module> => {
    ast::modules::ModuleDefItem::Module(<>)
  },
}

// Constants

pub(crate) ConstantDef: ast::constants::ConstantDef = {
  <is_pub:"pub"?> "const" <name:Ident> ":" <type_spec:TypeSpec> "=" <exp:Expression> => {
    ast::constants::ConstantDef {
      decl: ast::constants::ConstantDecl {
        doc_string: None,
        name,
        is_pub: is_pub.is_some(),
        r#type: type_spec
      },
      value: exp,
    }
  },
}

// -- Functions

pub(crate) FunctionRetType: ast::types::TypeSpec = {
  "->" <TypeSpec> => <>
}

pub(crate) Param: ast::functions::Param = {
  <name:Ident> ":" <param_type:TypeSpec> => ast::functions::Param {
    name,
    r#type: param_type
  }
}

pub(crate) FunctionDef: ast::functions::FunctionDef = {
  <is_pub:"pub"?> "fn" <name:Ident> <generic_params:GenericParams?> "(" <params:Comma<Param>> ")" <ret_type:FunctionRetType?> "{"
    <statements:StatementList>
  "}" => {
    ast::functions::FunctionDef {
      decl: ast::functions::FunctionDecl {
        doc_string: None,
        generic_params: generic_params.unwrap_or(vec![]),
        name,
        params,
        ret_type,
      },
      body: statements
    }
  }
}

// Expressions

pub(crate) Term: ast::expressions::Expression = {
  <SimpleExpr> => ast::expressions::Expression::Simple(<>),
  <FnCallOp> => ast::expressions::Expression::FnCall(<>),
  <MatchExpr> => ast::expressions::Expression::Match(<>),
  <IfExpr> => ast::expressions::Expression::If(<>),
  "(" <Expression> ")",
}

pub(crate) Expression: ast::expressions::Expression = {
  #[precedence(level="0")]
  <Term>,
  <op:UnaryOp> <rhs:Expression> => ast::expressions::Expression::UnaryOp(
    op,
    Box::new(rhs)
  ),
  // <op:UnaryOp> <e:Expression> => ast::expressions::Expression::UnaryOp(op, Box::new(e)),
  #[precedence(level="1")] #[assoc(side="left")]
  <lhs:Expression> <op:BinaryFirstLvlOp> <rhs:Expression> => ast::expressions::Expression::BinaryOp(
    Box::new(lhs),
    op,
    Box::new(rhs)
  ),
  #[precedence(level="2")] #[assoc(side="left")]
  <lhs:Expression> <op:BinarySecondLvlOp> <rhs:Expression> => ast::expressions::Expression::BinaryOp(
    Box::new(lhs),
    op,
    Box::new(rhs)
  ),
  #[precedence(level="3")] #[assoc(side="left")]
  <lhs:Expression> <op:BinaryThirdLvlOp> <rhs:Expression> => ast::expressions::Expression::BinaryOp(
    Box::new(lhs),
    op,
    Box::new(rhs)
  ),

}

pub BinaryFirstLvlOp: ast::expressions::BinaryOp = {
  "==" => ast::expressions::BinaryOp::Compare(ast::expressions::CmpOp::Eq),
  "!=" => ast::expressions::BinaryOp::Compare(ast::expressions::CmpOp::NotEq),
  "<" => ast::expressions::BinaryOp::Compare(ast::expressions::CmpOp::Lt),
  ">" => ast::expressions::BinaryOp::Compare(ast::expressions::CmpOp::Gt),
  "<=" => ast::expressions::BinaryOp::Compare(ast::expressions::CmpOp::LtEq),
  ">=" => ast::expressions::BinaryOp::Compare(ast::expressions::CmpOp::GtEq),
  "&&" => ast::expressions::BinaryOp::Logic(ast::expressions::LogicOp::And),
  "||" => ast::expressions::BinaryOp::Logic(ast::expressions::LogicOp::Or),
}

pub BinarySecondLvlOp: ast::expressions::BinaryOp = {
  "/" => ast::expressions::BinaryOp::Arith(ast::expressions::ArithOp::Div),
  "*" => ast::expressions::BinaryOp::Arith(ast::expressions::ArithOp::Mul),
  "%" => ast::expressions::BinaryOp::Arith(ast::expressions::ArithOp::Mod),
}

pub BinaryThirdLvlOp: ast::expressions::BinaryOp = {
  "+" => ast::expressions::BinaryOp::Arith(ast::expressions::ArithOp::Add),
  "-" => ast::expressions::BinaryOp::Arith(ast::expressions::ArithOp::Sub),
  "&" => ast::expressions::BinaryOp::Bitwise(ast::expressions::BitwiseOp::And),
  "|" => ast::expressions::BinaryOp::Bitwise(ast::expressions::BitwiseOp::Or),
  "^" => ast::expressions::BinaryOp::Bitwise(ast::expressions::BitwiseOp::Xor),
}

pub UnaryOp: ast::expressions::UnaryOp = {
  "-" => ast::expressions::UnaryOp::ArithNeg,
  "!" => ast::expressions::UnaryOp::LogicalNot,
  "~" => ast::expressions::UnaryOp::BitwiseNot,
}

pub(crate) SimpleExpr: ast::expressions::SimpleExpr = {
  <"integer"> => ast::expressions::SimpleExpr::ConstInt(<>),
  <"boolean"> => ast::expressions::SimpleExpr::ConstBool(<>),
  <"string"> => ast::expressions::SimpleExpr::ConstStr(<>),
  <PathOp> => ast::expressions::SimpleExpr::Path(<>),
}


pub(crate) IfExpr: ast::expressions::IfExpr = {
  "if" <value:Expression> "{" <contents:StatementList> "}"
    <else_stmts:("else" "{" <StatementList> "}")?> => {
    ast::expressions::IfExpr {
      value: Box::new(value),
      contents,
      r#else: else_stmts,
    }
  }
}

pub(crate) MatchExpr: ast::expressions::MatchExpr = {
  "match" <value:Expression> "{" <variants:Comma<MatchVariant>> "}" => {
    ast::expressions::MatchExpr {
      value: Box::new(value),
      variants
    }
  }
}

pub(crate) MatchVariant: ast::expressions::MatchVariant = {
  // 0 -> 1
  <case:SimpleExpr> "->" <stmt:Statement> => {
    ast::expressions::MatchVariant {
      case,
      block: vec![stmt]
    }
  },
  // x -> { ... }
  <case:SimpleExpr> "->" "{" <stmts:StatementList> "}" => {
    ast::expressions::MatchVariant {
      case,
      block: stmts
    }
  }
}

pub(crate) PathOp: ast::expressions::PathOp = {
  <first:Ident> <extra:PathSegments?> => ast::expressions::PathOp {
    first,
    extra: extra.unwrap_or(vec![]),
  }
}

pub(crate) PathSegment: ast::expressions::PathSegment = {
  "." <Ident> => ast::expressions::PathSegment::FieldAccess(<>),
  "[" <e:SimpleExpr> "]" => ast::expressions::PathSegment::ArrayIndex(e),
}

pub PathSegments: Vec<ast::expressions::PathSegment> = {
  <PathSegment> => vec![<>],
  <mut s:PathSegments> <n:PathSegment> => {
      s.push(n);
      s
  },
}

pub(crate) FnCallOp: ast::expressions::FnCallOp = {
  <target:Ident> "(" <args:Comma<Expression>> ")" => ast::expressions::FnCallOp {
    target,
    args
  }
}

// -- Statements

pub StatementList: Vec<ast::statements::Statement> = {
  <Statement> => vec![<>],
  <mut s:StatementList> <n:Statement> => {
      s.push(n);
      s
  },
}

pub(crate) Statement: ast::statements::Statement = {
  <MatchExpr> ";"? => ast::statements::Statement::Match(<>),
  <IfExpr> ";"? => ast::statements::Statement::If(<>),
  <WhileStmt> ";"? => ast::statements::Statement::While(<>),
  <LetStmt> ";" => ast::statements::Statement::Let(<>),
  <AssignStmt> ";" => ast::statements::Statement::Assign(<>),
  <FnCallOp> ";" => ast::statements::Statement::FnCall(<>),
  <ReturnStmt> ";"? => ast::statements::Statement::Return(<>),
}

pub(crate) LetStmt: ast::statements::LetStmt = {
  "let" <is_mutable:"mut"?> <name:Ident> ":" <target_type:TypeSpec> "=" <value:Expression> => ast::statements::LetStmt {
    is_mutable: is_mutable.is_some(),
    target: ast::statements::LetStmtTarget::Simple {
      name,
      r#type: target_type
    },
    value
  },
}

pub(crate) AssignStmt: ast::statements::AssignStmt = {
  <target:PathOp> "=" <value:Expression> => ast::statements::AssignStmt {
    target,
    value
  },
}

pub(crate) ReturnStmt: ast::statements::ReturnStmt = {
  "return" <value:Expression> => ast::statements::ReturnStmt {
    value,
  },
}

pub(crate) WhileStmt: ast::statements::WhileStmt = {
  "while" <value:Expression> "{" <contents:StatementList> "}" => {
    ast::statements::WhileStmt {
      value,
      contents,
    }
  }
}
