use crate::tokens::Token;
use crate::lexer::LexicalError;
use concrete_ast as ast;
use ast::common::Span;
use std::str::FromStr;


grammar;

extern {
  type Location = usize;
  type Error = LexicalError;

  enum Token {
    // keywords
    "let" => Token::KeywordLet,
    "const" => Token::KeywordConst,
    "fn" => Token::KeywordFn,
    "return" => Token::KeywordReturn,
    "struct" => Token::KeywordStruct,
    "union" => Token::KeywordUnion,
    "enum" => Token::KeywordEnum,
    "if" => Token::KeywordIf,
    "else" => Token::KeywordElse,
    "while" => Token::KeywordWhile,
    "for" => Token::KeywordFor,
    "match" => Token::KeywordMatch,
    "mod" => Token::KeywordMod,
    "pub" => Token::KeywordPub,
    "mut" => Token::KeywordMut,
    "import" => Token::KeywordImport,

    // literals
    "identifier" => Token::Identifier(<String>),
    "integer" => Token::Integer(<u128>),
    "float" => Token::Float(<String>),
    "string" => Token::String(<String>),
    "char" => Token::Char(<char>),
    "boolean" => Token::Boolean(<bool>),

    // Other

    "(" => Token::LeftParen,
    ")" => Token::RightParen,
    "{" => Token::LeftBracket,
    "}" => Token::RightBracket,
    "[" => Token::LeftSquareBracket,
    "]" => Token::RightSquareBracket,
    "=" => Token::Assign,
    ";" => Token::Semicolon,
    ":" => Token::Colon,
    "->" => Token::Arrow,
    "," => Token::Coma,
    "<" => Token::LessThanSign,
    ">" => Token::MoreThanSign,
    ">=" => Token::MoreThanEqSign,
    "<=" => Token::LessThanEqSign,
    "." => Token::Dot,

    // operators
    "+" => Token::OperatorAdd,
    "-" => Token::OperatorSub,
    "*" => Token::OperatorMul,
    "/" => Token::OperatorDiv,
    "%" => Token::OperatorRem,
    "&&" => Token::OperatorAnd,
    "||" => Token::OperatorOr,
    "==" => Token::OperatorEq,
    "!=" => Token::OperatorNe,
    "!" => Token::OperatorNot,
    "~" => Token::OperatorBitwiseNot,
    "^" => Token::OperatorBitwiseXor,
    "&" => Token::Ampersand,
    "|" => Token::OperatorBitwiseOr,
  }
}

// lalrpop macros

Dot<T>: Vec<T> = {
    <mut v:(<T> ".")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

SemiColon<T>: Vec<T> = {
    <mut v:(<T> ";")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

PlusSeparated<T>: Vec<T> = {
    <mut v:(<T> "+")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

// Requires the semicolon at end
SemiColonSeparated<T>: Vec<T> = {
  <T> ";" => vec![<>],
  <mut s:SemiColonSeparated<T>> <n:T> ";" => {
    s.push(n);
    s
  }
};

// -- Common

pub(crate) Ident: ast::common::Ident = {
  <lo:@L> <name:"identifier"> <hi:@R> => ast::common::Ident {
    name,
    span: ast::common::Span::new(lo, hi),
  }
}

pub(crate) RefType: ast::types::RefType = {
  "&" => ast::types::RefType::Borrow,
  "&" "mut" => ast::types::RefType::MutBorrow,
}

pub(crate) TypeSpec: ast::types::TypeSpec = {
  <lo:@L> <is_ref:RefType?> <name:Ident> <hi:@R> => ast::types::TypeSpec::Simple {
    name,
    is_ref,
    span: Span::new(lo, hi),
  },
  <lo:@L> <is_ref:RefType?> <name:Ident> "<" <type_params:Comma<TypeSpec>> ">" <hi:@R> => ast::types::TypeSpec::Generic {
    name,
    type_params,
    is_ref,
    span: Span::new(lo, hi),
  },
  <lo:@L> <is_ref:RefType?> "[" <of_type:TypeSpec> <size:(";" <"integer">)> "]"<hi:@R> => ast::types::TypeSpec::Array {
    of_type: Box::new(of_type),
    size: size.try_into().expect("size is too big"),
    is_ref,
    span: Span::new(lo, hi),
  }
}

pub(crate) GenericParam: ast::common::GenericParam = {
  <lo:@L> <name:Ident> <hi:@R> => ast::common::GenericParam {
    name,
    params: vec![],
    span: Span::new(lo, hi),
  },
  <lo:@L> <name:Ident> ":" <params: PlusSeparated<TypeSpec>> <hi:@R> => ast::common::GenericParam {
    name,
    params,
    span: Span::new(lo, hi),
  },
}

pub(crate) GenericParams: Vec<ast::common::GenericParam> = {
  "<" <Comma<GenericParam>> ">" => <>
}

// Module list

pub Program: ast::Program = {
  <Module> => {
    ast::Program {
      modules: vec![<>],
    }
  },
  <mut s:Program> <n:Module> => {
      s.modules.push(n);
      s
  },
}

// Modules

pub(crate) Module: ast::modules::Module = {
  <lo:@L> "mod" <name:Ident> "{" <imports:ImportList?> <contents:ModuleItems?> "}" <hi:@R> => {
    ast::modules::Module {
      doc_string: None,
      imports: imports.unwrap_or_else(Vec::new),
      name,
      contents: contents.unwrap_or_else(Vec::new),
      span: Span::new(lo, hi),
    }
  }
}

pub(crate) ImportList: Vec<ast::imports::ImportStmt> = {
  <ImportStmt> => vec![<>],
  <mut s:ImportList> <n:ImportStmt> => {
      s.push(n);
      s
  },
}


pub(crate) ImportStmt: ast::imports::ImportStmt = {
  <lo:@L> "import" <module:Dot<Ident>> "{" <symbols:Comma<Ident>> "}" ";" <hi:@R>  => {
    ast::imports::ImportStmt {
      module,
      symbols,
      span: Span::new(lo, hi),
    }
  }
}


pub(crate) ModuleItems: Vec<ast::modules::ModuleDefItem> = {
  <ModuleDefItem> => vec![<>],
  <mut s:ModuleItems> <n:ModuleDefItem> => {
      s.push(n);
      s
  },
}

pub(crate) ModuleDefItem: ast::modules::ModuleDefItem = {
  <ConstantDef> ";" => {
    ast::modules::ModuleDefItem::Constant(<>)
  },
  <StructDef> => {
    ast::modules::ModuleDefItem::Struct(<>)
  },
  <UnionDef> => {
    ast::modules::ModuleDefItem::Union(<>)
  },
  <EnumDef> => {
    ast::modules::ModuleDefItem::Enum(<>)
  },
  <FunctionDef> => {
    ast::modules::ModuleDefItem::Function(<>)
  },
  <Module> => {
    ast::modules::ModuleDefItem::Module(<>)
  },
}

// Constants

pub(crate) ConstantDef: ast::constants::ConstantDef = {
  <is_pub:"pub"?> "const" <name:Ident> ":" <type_spec:TypeSpec> "=" <exp:Expression> => {
    ast::constants::ConstantDef {
      decl: ast::constants::ConstantDecl {
        doc_string: None,
        name,
        is_pub: is_pub.is_some(),
        r#type: type_spec
      },
      value: exp,
    }
  },
}

// -- Functions

pub(crate) FunctionRetType: ast::types::TypeSpec = {
  "->" <TypeSpec> => <>
}

pub(crate) Param: ast::functions::Param = {
  <name:Ident> ":" <param_type:TypeSpec> => ast::functions::Param {
    name,
    r#type: param_type
  }
}

pub(crate) FunctionDef: ast::functions::FunctionDef = {
  <lo:@L> <is_pub:"pub"?> "fn" <name:Ident> <generic_params:GenericParams?> "(" <params:Comma<Param>> ")" <ret_type:FunctionRetType?> "{"
    <statements:StatementList?>
  "}" <hi:@R> => {
    ast::functions::FunctionDef {
      decl: ast::functions::FunctionDecl {
        doc_string: None,
        generic_params: generic_params.unwrap_or(vec![]),
        name,
        params,
        ret_type,
      },
      body: statements.unwrap_or_else(Vec::new),
      span: Span::new(lo, hi),
    }
  }
}

// Struct

pub(crate) StructField: ast::structs::Field = {
    <lo:@L> <name:Ident> ":" <r#type:TypeSpec> <hi:@R> => ast::structs::Field {
        name,
        r#type,
        span: Span::new(lo, hi),
    }
}

pub(crate) StructDef: ast::structs::StructDecl = {
    <lo:@L> "struct" <name:Ident> <generics:GenericParams?> "{" <fields:Comma<StructField>> "}" <hi:@R> => ast::structs::StructDecl {
        name,
        fields,
        generics: generics.unwrap_or(vec![]),
        span: Span::new(lo, hi),
    }
}


pub(crate) UnionDef: ast::enums::UnionDecl = {
    <lo:@L> "union" <name:Ident> <generics:GenericParams?> "{" <variants:Comma<StructField>> "}" <hi:@R> => ast::enums::UnionDecl {
        name,
        variants,
        generics: generics.unwrap_or(vec![]),
        span: Span::new(lo, hi),
    }
}

pub(crate) EnumDef: ast::enums::EnumDecl = {
    <lo:@L> "enum" <name:Ident> <generics:GenericParams?> "{" <variants:Comma<EnumVariant>> "}" <hi:@R> => ast::enums::EnumDecl {
        name,
        variants,
        generics: generics.unwrap_or(vec![]),
        span: Span::new(lo, hi),
    }
}


pub(crate) EnumVariant: ast::enums::EnumVariant = {
    <lo:@L> <name:Ident> <fields:("{" <Comma<StructField>> "}")?> <hi:@R> => ast::enums::EnumVariant {
        name,
        fields: fields.unwrap_or_default(),
        span: Span::new(lo, hi),
    }
}

pub(crate) StructInitField: (ast::common::Ident, ast::expressions::StructInitField) = {
    <lo:@L> <name:Ident> ":" <value:Expression> <hi:@R> => (name, ast::expressions::StructInitField {
        value,
        span: Span::new(lo, hi),
    }),
    <lo:@L> <name:Ident> ":" <value:StructInitExpr> <hi:@R> => (name, ast::expressions::StructInitField {
        value: ast::expressions::Expression::StructInit(value),
        span: Span::new(lo, hi),
    })
}

pub(crate) StructInitExpr: ast::expressions::StructInitExpr = {
  <lo:@L> <name:Ident> "{" <fields:Comma<StructInitField>> "}" <hi:@R> => ast::expressions::StructInitExpr {
    name,
    fields: fields.into_iter().collect(),
    span: Span::new(lo, hi),
  }
}

// Expressions

pub(crate) Term: ast::expressions::Expression = {
  #[precedence(level="0")]
  <lo:@L> <v:ValueExpr> <hi:@R> => ast::expressions::Expression::Value(v, Span::new(lo, hi)),
  <FnCallOp> => ast::expressions::Expression::FnCall(<>),
  <MatchExpr> => ast::expressions::Expression::Match(<>),
  <IfExpr> => ast::expressions::Expression::If(<>),
  "(" <StructInitExpr> ")" => ast::expressions::Expression::StructInit(<>),
  #[precedence(level="1")] #[assoc(side="left")]
  "(" <Expression> ")",
}

pub(crate) Expression: ast::expressions::Expression = {
  #[precedence(level="0")]
  <Term>,
  #[precedence(level="1")] #[assoc(side="left")]
  "&" "mut" <Expression> => ast::expressions::Expression::AsRef(Box::new(<>), ast::types::RefType::MutBorrow),
  "&" <Expression> => ast::expressions::Expression::AsRef(Box::new(<>), ast::types::RefType::Borrow),
  "*" <Expression> => ast::expressions::Expression::Deref(Box::new(<>)),
  <op:UnaryOp> <rhs:Expression> => ast::expressions::Expression::UnaryOp(
    op,
    Box::new(rhs)
  ),
  #[precedence(level="2")] #[assoc(side="left")]
  <lhs:Expression> <op:BinaryFirstLvlOp> <rhs:Expression> => ast::expressions::Expression::BinaryOp(
    Box::new(lhs),
    op,
    Box::new(rhs)
  ),
  #[precedence(level="3")] #[assoc(side="left")]
  <lhs:Expression> <op:BinarySecondLvlOp> <rhs:Expression> => ast::expressions::Expression::BinaryOp(
    Box::new(lhs),
    op,
    Box::new(rhs)
  ),
  #[precedence(level="4")] #[assoc(side="left")]
  <lhs:Expression> <op:BinaryThirdLvlOp> <rhs:Expression> => ast::expressions::Expression::BinaryOp(
    Box::new(lhs),
    op,
    Box::new(rhs)
  ),

}

pub BinaryFirstLvlOp: ast::expressions::BinaryOp = {
  "==" => ast::expressions::BinaryOp::Compare(ast::expressions::CmpOp::Eq),
  "!=" => ast::expressions::BinaryOp::Compare(ast::expressions::CmpOp::NotEq),
  "<" => ast::expressions::BinaryOp::Compare(ast::expressions::CmpOp::Lt),
  ">" => ast::expressions::BinaryOp::Compare(ast::expressions::CmpOp::Gt),
  "<=" => ast::expressions::BinaryOp::Compare(ast::expressions::CmpOp::LtEq),
  ">=" => ast::expressions::BinaryOp::Compare(ast::expressions::CmpOp::GtEq),
  "&&" => ast::expressions::BinaryOp::Logic(ast::expressions::LogicOp::And),
  "||" => ast::expressions::BinaryOp::Logic(ast::expressions::LogicOp::Or),
}

pub BinarySecondLvlOp: ast::expressions::BinaryOp = {
  "/" => ast::expressions::BinaryOp::Arith(ast::expressions::ArithOp::Div),
  "*" => ast::expressions::BinaryOp::Arith(ast::expressions::ArithOp::Mul),
  "%" => ast::expressions::BinaryOp::Arith(ast::expressions::ArithOp::Mod),
}

pub BinaryThirdLvlOp: ast::expressions::BinaryOp = {
  "+" => ast::expressions::BinaryOp::Arith(ast::expressions::ArithOp::Add),
  "-" => ast::expressions::BinaryOp::Arith(ast::expressions::ArithOp::Sub),
  "&" => ast::expressions::BinaryOp::Bitwise(ast::expressions::BitwiseOp::And),
  "|" => ast::expressions::BinaryOp::Bitwise(ast::expressions::BitwiseOp::Or),
  "^" => ast::expressions::BinaryOp::Bitwise(ast::expressions::BitwiseOp::Xor),
}

pub UnaryOp: ast::expressions::UnaryOp = {
  "-" => ast::expressions::UnaryOp::ArithNeg,
  "!" => ast::expressions::UnaryOp::LogicalNot,
  "~" => ast::expressions::UnaryOp::BitwiseNot,
}

pub(crate) ValueExpr: ast::expressions::ValueExpr = {
  <"integer"> => ast::expressions::ValueExpr::ConstInt(<>),
  <"float"> => ast::expressions::ValueExpr::ConstFloat(<>),
  <"boolean"> => ast::expressions::ValueExpr::ConstBool(<>),
  <"string"> => ast::expressions::ValueExpr::ConstStr(<>),
  <"char"> => ast::expressions::ValueExpr::ConstChar(<>),
  <PathOp> => ast::expressions::ValueExpr::Path(<>),
}

pub(crate) IfExpr: ast::expressions::IfExpr = {
  <lo:@L> "if" <value:Expression> "{" <contents:StatementList> "}"
    <else_stmts:("else" "{" <StatementList> "}")?> <hi:@R> => {
    ast::expressions::IfExpr {
      value: Box::new(value),
      contents,
      r#else: else_stmts,
      span: Span::new(lo, hi)
    }
  }
}

pub(crate) MatchExpr: ast::expressions::MatchExpr = {
  <lo:@L> "match" <value:Expression> "{" <variants:Comma<MatchVariant>> "}" <hi:@R> => {
    ast::expressions::MatchExpr {
      value: Box::new(value),
      variants,
      span: Span::new(lo, hi)
    }
  }
}

pub(crate) MatchVariant: ast::expressions::MatchVariant = {
  // 0 -> 1
  <lo:@L> <case:ValueExpr> "->" <stmt:Statement> <hi:@R> => {
    ast::expressions::MatchVariant {
      case,
      block: vec![stmt],
      span: Span::new(lo, hi)
    }
  },
  // x -> { ... }
  <lo:@L> <case:ValueExpr> "->" "{" <stmts:StatementList> "}" <hi:@R> => {
    ast::expressions::MatchVariant {
      case,
      block: stmts,
      span: Span::new(lo, hi)
    }
  }
}

pub(crate) PathOp: ast::expressions::PathOp = {
  <lo:@L> <first:Ident> <extra:PathSegments?> <hi:@R> => ast::expressions::PathOp {
    first,
    extra: extra.unwrap_or(vec![]),
    span: Span::new(lo, hi),
  },
}

pub(crate) PathSegment: ast::expressions::PathSegment = {
  <lo:@L> "." <e:Ident> <hi:@R> => ast::expressions::PathSegment::FieldAccess(e, Span::new(lo, hi)),
  <lo:@L> "[" <e:ValueExpr> "]" <hi:@R> => ast::expressions::PathSegment::ArrayIndex(e, Span::new(lo, hi)),
}

pub PathSegments: Vec<ast::expressions::PathSegment> = {
  <PathSegment> => vec![<>],
  <mut s:PathSegments> <n:PathSegment> => {
      s.push(n);
      s
  },
}

pub(crate) FnCallOp: ast::expressions::FnCallOp = {
  <lo:@L> <target:Ident> "(" <args:Comma<Expression>> ")" <hi:@R> => ast::expressions::FnCallOp {
    target,
    args,
    span: Span::new(lo, hi),
  }
}

// -- Statements

pub StatementList: Vec<ast::statements::Statement> = {
  <Statement> => vec![<>],
  <mut s:StatementList> <n:Statement> => {
      s.push(n);
      s
  },
}

pub(crate) Statement: ast::statements::Statement = {
  <MatchExpr> ";"? => ast::statements::Statement::Match(<>),
  <IfExpr> ";"? => ast::statements::Statement::If(<>),
  <WhileStmt> ";"? => ast::statements::Statement::While(<>),
  <LetStmt> ";" => ast::statements::Statement::Let(<>),
  <AssignStmt> ";" => ast::statements::Statement::Assign(<>),
  <FnCallOp> ";" => ast::statements::Statement::FnCall(<>),
  <ReturnStmt> ";" => ast::statements::Statement::Return(<>),
}

pub(crate) LetStmt: ast::statements::LetStmt = {
  <lo:@L> "let" <is_mutable:"mut"?> <name:Ident> ":" <target_type:TypeSpec> "=" <value:Expression> <hi:@R> => ast::statements::LetStmt {
    is_mutable: is_mutable.is_some(),
    target: ast::statements::LetStmtTarget::Simple {
      name,
      r#type: target_type
    },
    value,
    span: Span::new(lo, hi),
  },
  <lo:@L> "let" <is_mutable:"mut"?> <name:Ident> ":" <target_type:TypeSpec> "=" <value:StructInitExpr> <hi:@R> => ast::statements::LetStmt {
    is_mutable: is_mutable.is_some(),
    target: ast::statements::LetStmtTarget::Simple {
      name,
      r#type: target_type
    },
    value: ast::expressions::Expression::StructInit(value),
    span: Span::new(lo, hi),
  },
}

pub(crate) AssignStmt: ast::statements::AssignStmt = {
  <lo:@L> <derefs:"*"*> <target:PathOp> "=" <value:Expression> <hi:@R> => ast::statements::AssignStmt {
    target,
    value,
    derefs: derefs.len(),
    span: Span::new(lo, hi),
  },
   <lo:@L> <derefs:"*"*> <target:PathOp> "=" <value:StructInitExpr> <hi:@R> => ast::statements::AssignStmt {
    target,
    value: ast::expressions::Expression::StructInit(value),
    derefs: derefs.len(),
    span: Span::new(lo, hi),
  },
}

pub(crate) ReturnStmt: ast::statements::ReturnStmt = {
  <lo:@L> "return" <value:Expression?> <hi:@R> => ast::statements::ReturnStmt {
    value,
    span: Span::new(lo, hi),
  },
}

pub(crate) WhileStmt: ast::statements::WhileStmt = {
  "while" <value:Expression> "{" <contents:StatementList> "}" => {
    ast::statements::WhileStmt {
      value,
      contents,
    }
  }
}
